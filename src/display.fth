HEX
8800 CONSTANT VDPRD
8802 CONSTANT VDPSTS
8C00 CONSTANT VDPWD
8C02 CONSTANT VDPWA

4000 CONSTANT WRITEMASK
0100 CONSTANT SHIFTRIGHTONEBYTE
E0   CONSTANT GRAPHICSMODE

4                      CONSTANT SCREENIMAGETABLE
SCREENIMAGETABLE 400 * CONSTANT SCREENIMAGEADDRESS
DECIMAL

\ TODO: Need to set LIMI 0 & LIMI 2 before calling this
: SETVDPWRITEADDRESS ( Vaddr -- )
  WRITEMASK OR
  DUP SHIFTRIGHTONEBYTE / VDPWA !
  VDPWA !
;

: TOWERLABELS$ ( -- addr len )
  S" TOWER 1     TOWER 2     TOWER 3"
;

: GETTOWERCHAR ( toweraddr index -- )
  + C@ CASE
    32 OF 32 ENDOF       \ empty
    82 OF 128 ENDOF      \ red
    71 OF 136 ENDOF      \ green
    66 OF 144 ENDOF      \ blue
    32
  ENDCASE
;

: DISPLAYBOARD ( -- )
  5 0 DO
    4 I - TOWER1 GETTOWERCHAR
    I 32 * 3 + SCREENIMAGEADDRESS + VC!
    4 I - TOWER2 GETTOWERCHAR
    I 32 * 15 + SCREENIMAGEADDRESS + VC!
    4 I - TOWER3 GETTOWERCHAR
    I 32 * 27 + SCREENIMAGEADDRESS + VC!
  LOOP
  
  TOWERLABELS$ SCREENIMAGEADDRESS 160 + SWAP VWRITE
;

\ Use this to display the board when FORTH is in interactive mode
: TESTDISPLAY ( -- )
  16 7 7 COLOR
  17 13 13 COLOR
  18 5 5 COLOR
  GRAPHICSMODE 1 VWTR
  SCREENIMAGETABLE 2 VWTR

  DISPLAYBOARD

  KEY DROP
  0 2 VWTR
;