HEX
8379 CONSTANT VDPTIMER
8800 CONSTANT VDPRD
8802 CONSTANT VDPSTS
8C00 CONSTANT VDPWD
8C02 CONSTANT VDPWA

4000 CONSTANT WRITEMASK
0100 CONSTANT SHIFTRIGHTONEBYTE
E0   CONSTANT GRAPHICSMODE

4                      CONSTANT SCREENIMAGETABLE
SCREENIMAGETABLE 400 * CONSTANT SCREENIMAGEADDRESS

20   CONSTANT CURSORFREQUENCY
DECIMAL

\ TODO: Need to set LIMI 0 & LIMI 2 before calling this
: SETVDPWRITEADDRESS ( Vaddr -- )
  WRITEMASK OR
  DUP SHIFTRIGHTONEBYTE / VDPWA !
  VDPWA !
;

: TOWERLABELS$ ( -- addr len )
  S" TOWER 1     TOWER 2     TOWER 3"
;

: GETTOWERCHAR ( toweraddr index -- )
  + C@ CASE
    32 OF 32 ENDOF       \ empty
    82 OF 128 ENDOF      \ red
    71 OF 136 ENDOF      \ green
    66 OF 144 ENDOF      \ blue
    32
  ENDCASE
;

: DISPLAYBOARD ( -- )
  5 0 DO
    4 I - TOWER1 GETTOWERCHAR
    I 32 * 3 + SCREENIMAGEADDRESS + VC!
    4 I - TOWER2 GETTOWERCHAR
    I 32 * 15 + SCREENIMAGEADDRESS + VC!
    4 I - TOWER3 GETTOWERCHAR
    I 32 * 27 + SCREENIMAGEADDRESS + VC!
  LOOP
  
  TOWERLABELS$ SCREENIMAGEADDRESS 160 + SWAP VWRITE
;

: INSTRUCTIONS$ ( -- addr len )
  S" TYPE TWO TOWERS TO MOVE A COLOR"
;

: FROM$ ( -- addr len )
  S" FROM  "
;

: TO$ ( -- addr len )
  S" TO    "
;

: WIN$
  S" YOU WIN. PRESS ANY KEY."
;

: ERRORRANGE$ ( -- addr len )
  S" MUST BE A NUMBER IN RANGE 1 TO 3"
;

: ERRORMOVE$ ( -- addr len )
  S" CAN'T MOVE BETWEEN THOSE TOWERS RIGHT NOW"
;

( Output should be either 1, 2, or 3 )
: ACCEPTCHAR ( vdpAddr -- n1 )
  0
  BEGIN
    DROP
    ( flash cursor )
    VDPTIMER C@ CURSORFREQUENCY AND IF
      32 OVER VC!
    ELSE
      95 OVER VC!
    THEN
  KEY? DUP 48 > OVER 52 < AND UNTIL
  ( display the requested character at "vdpAddr", )
  ( leaving a copy of the char at the top of the stack. )
  DUP ROT VC!
  48 -
;

: ACCEPTINPUT ( -- fromTower toTower )
  INSTRUCTIONS$ SCREENIMAGEADDRESS 256 + SWAP VWRITE
  FROM$ SCREENIMAGEADDRESS 322 + SWAP VWRITE
  TO$ SCREENIMAGEADDRESS 354 + SWAP VWRITE
  SCREENIMAGEADDRESS 327 + ACCEPTCHAR
  SCREENIMAGEADDRESS 359 + ACCEPTCHAR
  BEEP
;

: DISPLAYWIN
  SCREENIMAGEADDRESS 256 + 160 32 VFILL
  WIN$ SCREENIMAGEADDRESS 256 + SWAP VWRITE
;

: GAMESTART
  16 7 7 COLOR
  17 13 13 COLOR
  18 5 5 COLOR
  GRAPHICSMODE 1 VWTR
  SCREENIMAGETABLE 2 VWTR
;

: GAMEEND
  KEY DROP
  0 2 VWTR
;

\
\ Display the board when FORTH is in interactive mode
\
: TESTDISPLAY ( -- )
  GAMESTART
  DISPLAYBOARD
  GAMEEND
;

: TESTINPUT ( -- )
  GAMESTART
  ACCEPTINPUT
  GAMEEND
;

: TESTGAME ( -- )
  GAMESTART
  BEGIN
    DISPLAYBOARD
    ACCEPTINPUT
    MOVECOLOR
  ARETOWERSCOMPLETE UNTIL
  DISPLAYBOARD
  DISPLAYWIN
  BEEP BEEP
  GAMEEND
;